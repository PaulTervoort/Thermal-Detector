plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'nl.paultervoort.thermaldetector'

    defaultConfig {
        applicationId "nl.paultervoort.thermaldetector"
        minSdk 28
        targetSdk 36
        compileSdkVersion 36
        versionCode 1
        versionName "1.0"
        resConfigs("en", "nl")
    }

    // Use local keystore.properties for signing if it can be loaded
    def releaseSigningConfig = null
    try {
        signingConfigs {
            release {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
                storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
                storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
                keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
                keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
        releaseSigningConfig = signingConfigs.release
    }
    catch (Exception ignored) {}

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig releaseSigningConfig
        }
    }

    splits {
        abi {
            enable true
            universalApk false
            reset()

            // Only ABIs that the FLIR SDK supports
            include "x86_64", "armeabi-v7a", "arm64-v8a"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }
}

dependencies {
    implementation(group: "com.flir", version: "2.15.0", name: 'androidsdk')
    implementation(group: "com.flir", version: "2.15.0", name: 'thermalsdk')

    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}